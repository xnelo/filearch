plugins {
    id 'nu.studer.jooq' version "$JOOQ_GENERATOR_VERSION"
}

dependencies {
    implementation "org.jooq:jooq:$JOOQ_VERSION"
    jooqGenerator "org.postgresql:postgresql:$JOOQ_GENERATOR_POSTGRES_DRIVER_VERSION"
}

spotlessCheck {
    enabled = false
}

String DB_HOST_ADDRESS = System.getenv("FILEARCH_DB_HOST_ADDRESS") ?: "localhost"

jooq {
    version = "$JOOQ_VERSION"
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // the default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = false

            generationTool {
                //logging = Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://' + DB_HOST_ADDRESS + ':5432/filearch'
                    user = 'xnelo'
                    password = 'buttmunch'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'FILEARCH'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.xnelo.filearch.jooq'
                        directory = 'src/main/java'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}